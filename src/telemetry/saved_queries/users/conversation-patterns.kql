// User Conversation Patterns
// Messages per user session analysis from OpenTelemetry events
dependencies
| where timestamp > ago(7d)
| where name == "Custom Event" and customDimensions["event.name"] == "AI_Response_Sent"
| extend 
    UserId = tostring(customDimensions["userId"]),
    ConversationId = tostring(customDimensions["conversationId"])
| where isnotempty(UserId) and UserId != "unknown" and isnotempty(ConversationId)
| summarize MessageCount = count() by UserId, ConversationId
| summarize 
    SingleMessage = countif(MessageCount == 1),
    ShortConvo = countif(MessageCount >= 2 and MessageCount <= 5),
    MediumConvo = countif(MessageCount >= 6 and MessageCount <= 15),
    LongConvo = countif(MessageCount > 15),
    TotalConversations = count(),
    AvgMessagesPerConvo = avg(MessageCount)
