// Retry Pattern Analysis
// Look for retry patterns in AI requests from OpenTelemetry events
dependencies
| where timestamp > ago(7d) 
| where name == "Custom Event" and customDimensions["event.name"] in ("External_AI_Request", "External_AI_Success")
| extend 
    UserId = tostring(customDimensions["userId"]),
    ConversationId = tostring(customDimensions["conversationId"]),
    SessionId = tostring(customDimensions["sessionId"])
| where isnotempty(UserId) and isnotempty(ConversationId)
| summarize RequestCount = count() by UserId, ConversationId, SessionId, bin(timestamp, 1m)
| where RequestCount > 1  // Multiple requests within same minute = potential retry
| extend RetryCount = RequestCount - 1
| summarize 
    RetryUsers = dcount(UserId), 
    TotalRetries = sum(RetryCount),
    AvgRetriesPerUser = avg(RetryCount)
by bin(timestamp, 1h)
| render timechart title="AI Request Retry Patterns"
